apply plugin: 'maven'

def group = 'com.openking.components'
def artifact = 'order'
def versionStr = "$PERSONAL_VERSION"
def type = 'aar'

if (project.hasProperty("isRelease") && project.property("isRelease") == "true") {
    versionStr = versionStr.replace("-SNAPSHOT", "")
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (rootProject.ext.find('localPath') != null) {
                repository(url: uri("${rootProject.ext.localPath}"))
                snapshotRepository(url: uri("${rootProject.ext.localPath}"))
            } else if (project.hasProperty("isRelease") && project.property("isRelease") == "true") {
                repository(url: MAVEN_REPO_RELEASE_URL) {
                    authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
                }
            } else {
                snapshotRepository(url: MAVEN_REPO_SNAPSHOT_URL) {
                    authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
                }
            }
            pom.project {
                version versionStr
                artifactId artifact
                groupId group
                packaging type
                description 'order_lib maven'
            }
        }
    }
}
//打包源码
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}